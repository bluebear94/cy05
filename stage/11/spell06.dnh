#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 61;
let level = 11;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(9000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(75);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

function createStar(x, y, s, t, a, b, l, w, av, g, d) {
	task spin(br) {
		while (!Obj_IsDeleted(br)) {
			ObjMove_SetPosition(br, ObjMove_GetX(first), ObjMove_GetY(first));
			ObjStLaser_SetAngle(br, ObjStLaser_GetAngle(br) + av);
			yield;
		}
	}
	let first;
	ascent (i in 0 .. b) {
		let br = CreateStraightLaserA1(x, y, a + 360 * i / b, l, w, 9999, g, d);
		if (i == 0) {first = br;}
		spin(br);
	}
	ObjMove_SetSpeed(first, s);
	ObjMove_SetAngle(first, t);
	return first;
}

task clockTask(shot, t) {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	wait(60);
	let u = rand(0, 360);
	let c = 0;
	while (!Obj_IsDeleted(shot)) {
		ObjMove_SetPosition(shot, cx + 2 * cy * cos(t) / 3, cy + 2 * cy * sin(t) / 3);
		if (c % 30 == 0) {
			CreateShotOA1(shot, 1.5, u, DS_BALL_S_YELLOW, 0);
		}
		t += 0.25;
		u += 2.3;
		c++;
		yield;
	}
}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let x = ObjMove_GetX(objBoss);
	let y = ObjMove_GetY(objBoss);
	let d = getDifficulty - 5;
	let clockCount = 4 + 3 * d;
	let t = 0;
	loop (clockCount) {
		let clock = createStar(cx + 2 * cy * cos(t) / 3, cy + 2 * cy * sin(t) / 3, 0, 0, 0.5 * t, 4, cy / 2, 20, 0.5, DS_BEAM_YELLOW, 60);
		clockTask(clock, t);
		t += 360 / clockCount;
	}
	while (!isDead) {
		let a = GetAngleToPlayer(objBoss);
		loop (5) {
			CreateShotOA1(objBoss, rand(1.5, 1.7), a + rand(-90, 90), DS_STAR_M_YELLOW, 0);
		}
		wait(60);
	}
}