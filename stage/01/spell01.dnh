#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp1"]
#Text["First spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
let id = 1;
let level = 1;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(5000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(32);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

task TWork {
	task trail(shot, angle) {
		whipSound;
		while (!Obj_IsDeleted(shot)) {
			ObjShot_SetDeleteFrame(CreateShotOA1(shot, 0, angle, DS_ICE_GREEN, 0), 600);
			yield;
		}
	}
	task zigzag(shot, angle) {
		while (!Obj_IsDeleted(shot)) {
			ObjMove_SetAngle(shot, angle + 90);
			loop (60) {yield;}
			ObjMove_SetAngle(shot, angle);
			loop (60) {yield;}
		}
	}
	if (getDifficulty >= 4) {TWorkOD;}
	let period = 120 - 15 * getDifficulty;
	let speed = 5 + 3 * getDifficulty;
	if (getDifficulty > 3) {period += 6 * (getDifficulty - 3); speed -= 2 * getDifficulty - 3;}
	let zzButterflyQty = 8 * getDifficulty;
	let zzAngularOffset = 45;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let ex = ObjMove_GetX(objBoss);
		let ex2 = ex + rand(-100, 100);
		let ey = ObjMove_GetY(objBoss);
		let a =  rand(-20, 20) + GetAngleToPlayer(objBoss);
		DelayLaser(ex2, ey, a, 600, 18, period / 2, DS_BALL_S_GREEN, period / 2);
		wait(period / 2);
		if (ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) {return;}
		let s = CreateShotA1(ex2, ey,
			speed, a, DS_BUTTERFLY_GREEN, 0);
		trail(s, a);
		wait(period * 0.25);
		let r = rand(0, 360);
		ascent(i in 0 .. zzButterflyQty) {
			let a = (360 / zzButterflyQty) * i + r;
			let x = ex + 30 * cos(a);
			let y = ey + 30 * sin(a);
			let s = CreateShotA1(x, y, 3, a + zzAngularOffset, DS_BUTTERFLY_YELLOW, 0);
			zigzag(s, a + zzAngularOffset);
		}
		shotSound;
		zzAngularOffset *= -1;
		wait(period * 0.75);
	}
}

task TWorkOD {
	let width = GetStgFrameWidth;
	let interval = width / 9;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		ascent (i in 0 .. 8) {
			let x = interval * (0.5 + i);
			if (ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) {return;}
			ascent(j in 0 .. 30) {
				if (ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) {return;}
				CreateShotA1(x, 100, 1, j * 12 + rand(-12, 12), DS_ICE_SKY, 0);
			}
			wait(20);
		}
		wait(180);
	}
}
