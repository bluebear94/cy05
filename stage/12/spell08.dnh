#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 73;
let level = 12;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"
#include "./bossfunc.dnh"

let height = GetStgFrameHeight;
let width = GetStgFrameWidth;

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(20000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(140);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

task lasers {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let cl = 0;
	while (!isDead) {
		let a = rand(-120, -60);
		ascent (i in -1 .. 2) {
			CreateStraightLaserA1(cx, height, a + 30 * i, 600, 24, 120, DS_BEAM_RED + cl, 60);
		}
		cl = (cl + 1) % 8;
		wait(180);
	}
}

task starTask(shot) {
	let sp = ObjMove_GetSpeed(shot);
	let width = GetStgFrameWidth;
	wait(rand(0, width / sp));
	ObjMove_SetAngle(shot, rand(60, 120));
}

task spawnStars {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let speed = 1.5 + 0.2 * getDifficulty;
	while (!isDead) {
		let s = randomSign;
		let y = 16 * randIntH(1, 10);
		let x = cx * (1 + s);
		ascent (i in 0 .. randIntH(3, 7)) {
			starTask(CreateShotA1(x, y, speed, 90 + 90 * s, DS_STAR_S_RED + randIntH(0, 8), 20 * i));
		}
		wait(40);
	}
}

task plantTree(x) {
	let lifespan = 24 * getDifficulty + rand(0, 90) + rand(0, 90);
	task stem(x, y, t, a, gen) {
		ascent (i in 0 .. a) {
			if (isDead) {return;}
			ObjShot_SetDeleteFrame(CreateShotA1(x, y, 0, t, DS_ICE_GREEN, 30), lifespan);
			x += 10 * cos(t);
			y += 10 * sin(t);
			wait(3);
		}
		loop (randIntH(2, 5)) {
			if (rand(0, 1) < 1.3 * 0.6 ^ gen) {
				stem(x, y, t + rand(-30, 30), a * rand(0.7, 0.8), gen + 1);
			}
		}
	}
	stem(x, GetStgFrameHeight, rand(-85, -95), rand(12, 16), 0);
}

task plantTrees {
	while (!isDead) {
		plantTree(rand(0, GetStgFrameWidth));
		wait(180);
	}
}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let d = getDifficulty - 5;
	let speed = 0.1 + 0.05 * d;
	lasers;
	spawnStars;
	plantTrees;
	while (!isDead) {
		ascent (i in 0 .. 4) {
			if (isDead) {return;}
			let a = GetAngleToPlayer(objBoss);
			let t = rand(0, 360);
			let x = rand(0, width);
			let y = rand(0, height / 2.5);
			loop (15) {
				CreateShotA1(x, y, 1.4 + speed * i, a, [DS_BILL_GREEN, DS_BILL_YELLOW][i % 2], 0);
				a += 24;
			}
			shotSound;
			wait(20);
		}
		wait(120);
	}
}