#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 71;
let level = 12;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"
#include "./bossfunc.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(12000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(90);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

let howl = false;

task wolfTask(shot, radius) {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let a = 0;
	ObjShot_SetSpellResist(shot, true);
	ObjShot_SetAutoDelete(shot, false);
	let c = 0;
	while (!Obj_IsDeleted(shot)) {
		ObjMove_SetPosition(shot, cx + radius * cos(a), cy + radius * sin(a));
		if (!howl && c % 60 == 0) {
			CreateShotOA1(shot, 2.5, GetAngleToPlayer(shot), DS_STAR_S_RED, 0);
		}
		if (howl && c % 5 == 0) {
			CreateShotOA1(shot, rand(1.8, 2), rand(0, 360), DS_BALL_S_ORANGE, 0);
		}
		a += 0.5;
		c++;
		yield;
	}
}

task createWolf(radius) {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	wolfTask(
		CreateShotA1(cx + radius, cy, 0, 0, DS_BALL_L_WHITE, 0),
		radius);
}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let x = ObjMove_GetX(objBoss);
	let y = ObjMove_GetY(objBoss);
	let d = getDifficulty - 5;
	let period = 5 - d;
	let maxRadius = (cx ^ 2 + cy ^ 2) ^ 0.5;
	ascent (i in 0 .. 5) {
		createWolf(maxRadius * i / 4);
	}
	while (!isDead) {
		let p = randIntH(3, 8);
		loop (p) {
			if (isDead) {return;}
			let a = rand(0, 360);
			ascent (i in 0 .. 40) {
				CreateShotOA1(objBoss, 2.5 * astroid(i * 9, 4), i * 9 + a, DS_BALL_S_WHITE, 0);
			}
			shotSound;
			wait(16 * period);
		}
		howl = true;
		roar;
		let q = truncate(rand(30, 60) * 5 / period);
		loop (q) {
			if (isDead) {return;}
			CreateShotOA1(objBoss, rand(1.8, 2), rand(0, 360), DS_BALL_S_ORANGE, 30);
			wait(period);
		}
		howl = false;
	}
}