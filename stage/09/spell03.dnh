#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 40;
let level = 9;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(8500);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(90);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

let MAX_SHOTS = 35 + 15 * getDifficulty;
let sc = 0;

task shotTask(shot) {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	ObjShot_SetSpellResist(shot, true);
	while (sc != MAX_SHOTS) {yield;}
	ObjMove_SetSpeed(shot, rand(1, 3));
	loop {
		let x = ObjMove_GetX(shot);
		let y = ObjMove_GetY(shot);
		if (x < 0 || x > width || y < 0 || y > height) {
			break;
		}
		yield;
	}
	ObjMove_SetSpeed(shot, 0);
	sc--;
	while (sc != 0) {
		ObjMove_SetAngle(shot, GetAngleToPlayer(shot));
		yield;
	}
	ObjShot_SetGraphic(shot, DS_RICE_S_GREEN);
	ObjMove_SetSpeed(shot, 1.5);
}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2 - 100;
	let d = getDifficulty;
	TWorkOD;
	while (!isDead) {
		let a = 90;
		loop (MAX_SHOTS) {
			if (isDead) {return;}
			let shot = CreateShotA1(
				ObjMove_GetX(objBoss) + 65 * cos(a), ObjMove_GetY(objBoss) + 65 * sin(a),
				0, rand(0, 360), DS_RICE_S_YELLOW, 30
			);
			shotTask(shot);
			a += 360 / MAX_SHOTS;
			wait(120 / MAX_SHOTS);
			sc++;
		}
		while (sc != 0) {yield;}
		ObjMove_SetDestAtFrame(objBoss, rand(65, width - 65), rand(65, 130), 60);
		wait(60);
	}
}

task TWorkOD {
	let s = -1;
	while (!isDead) {
		dragV(s, 60);
		s *= -1;
		wait(137);
	}
}