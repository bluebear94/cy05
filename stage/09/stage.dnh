#TouhouDanmakufu[Stage]
#ScriptVersion[3]
#Title["St9"]
#Text["Ninth (but not final) stage!"]
#Background["./stg_back.dnh"]
#Player["./../../player/player0.dnh", "./../../player/player2.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../system/healthbar.dnh"
let stage = "09";
let done = false;
let dialogueData;
let dialogueData2;
let bgm = 17;
#include "./../stage_header.dnh"

let width = GetStgFrameWidth;
let height = GetStgFrameHeight;

task TStage {
	if (getCharacter < 2) {
		dialogueData = readDD("d9_1");
		dialogueData2 = readDD("d9_2");
	}
	else {
		dialogueData = readDD("d9_3");
		dialogueData2 = readDD("d9_4");
	}
	let d = getDifficulty;
	let dc41 = 5 + truncate(d / 1.5);
	let dp41 = 20 - truncate(d / 0.75);
	let ds41 = 1.5;
	loop (10) {
		spawnEnemy39;
		wait(20);
	}
	wait(60);
	spawnEnemy40(150, 0, 150, 300);
	spawnEnemy40(width - 150, 0, width - 150, 300);
	waitForClearScreen;
	wait(60);
	showStageTitle([255, 192, 255]);
	ascent (i in 0 .. 4) {
		spawnEnemy40(30 + 60 * i, height, 30 + 60 * i, height - 50);
		spawnEnemy40(width - 30 - 60 * i, 0, width - 30 - 60 * i, 50);
		wait(90);
	}
	waitForClearScreen;
	wait(60);
	spawnEnemies41(0, 60, ds41, 10, dc41, dp41);
	wait(60);
	spawnEnemies41(width, 120, ds41, -170, dc41, dp41);
	wait(60);
	spawnEnemies41(width, height - 120, ds41, 170, dc41, dp41);
	wait(60);
	spawnEnemies41(0, height - 60, ds41, -10, dc41, dp41);
	waitForClearScreen;
	wait(60);
	spawnEnemies41(0, 60, ds41, 10, dc41 / 2, dp41);
	spawnEnemies41(width, 60, ds41, 170, dc41 / 2, dp41);
	wait(30);
	spawnEnemy42(0, 0, 90, 90, 0, 1);
	spawnEnemy42(width, 0, width - 90, 90, 180, -1);
	waitForClearScreen;
	wait(60);
	spawnEnemies41(0, 30, ds41, 30, dc41 / 2, dp41);
	spawnEnemies43(width, 110, 180, dc41 * 0.75, dp41);
	waitForClearScreen;
	wait(60);
	spawnEnemies41(width, 110, ds41, 150, dc41 / 2, dp41);
	spawnEnemies43(0, 30, 0, dc41 * 0.75, dp41);
	spawnEnemy42(0, 0, 90, 90, 0, 1);
	spawnEnemy42(width, 0, width - 90, 90, 180, -1);
	spawnEnemy40(width / 2, 0, width / 2, 100);
	spawnEnemy40(0, 150, 100, 150);
	spawnEnemy40(width, 150, width - 100, 150);
	waitForClearScreen;
	wait(60);
	spawnEnemy40(width / 2, 0, width / 2, 150);
	spawnEnemy40(0, 150, 100, 100);
	spawnEnemy40(width, 150, width - 100, 100);
	ascent (i in 0 .. 9) {
		spawnEnemy44a(width * (0.1 + 0.1 * i));
		wait(30);
	}
	spawnEnemies43(0, 50, 0, dc41 * 0.75, dp41);
	spawnEnemies43(width, 50, 180, dc41 * 0.75, dp41);
	descent (i in 0 .. 9) {
		spawnEnemy44a(width * (0.1 + 0.1 * i));
		wait(30);
	}
	waitForClearScreen;
	wait(60);
	spawnEnemies41(width / 2, 0, ds41, 90, dc41 * 0.75, dp41);
	spawnEnemies41(width / 3, height, ds41, -90, dc41 * 0.65, dp41);
	spawnEnemies41(2 * width / 3, height, ds41, -90, dc41 * 0.65, dp41);
	spawnEnemy44b(width * 0.25);
	spawnEnemy44b(width * 0.75);
	wait(120);
	spawnEnemy40(width / 2, 0, width / 2, 150);
	waitForClearScreen;
	wait(120);
	ascent (i in 0 .. 9) {
		spawnEnemy44a(width * (0.1 + 0.1 * i));
		spawnEnemy44b(width * (0.1 + 0.1 * i));
	}
	loop (32) {
		spawnEnemy39;
	}
	wait(60);
	spawnEnemy40(width / 2, height, width / 2, 300);
	spawnEnemy40(0, 300, 100, 350);
	spawnEnemy40(width, 300, width - 100, 350);
	waitForClearScreen;
	wait(120);
	task fork {
		loop (5) {
			spawnEnemy42(0, 0, 90, 90, 0, 1);
			spawnEnemy42(width, 0, width - 90, 90, 180, -1);
			wait(90);
		}
	}
	fork;
	let s = false;
	ascent (i in 0 .. 10) {
		spawnEnemies41(width * s, 20 + 30 * i, ds41, 180 * s, dc41 * 0.8, dp41);
		wait(45);
		s = !s;
	}
	waitForClearScreen;
	wait(120);
	ascent (i in 0 .. 10) {
		spawnEnemies43(width * s, height - 20 - 30 * i, 180 * s, dc41 * 0.3, dp41);
		wait(45);
		s = !s;
	}
	waitForClearScreen;
	wait(120);
	loop (10) {
		spawnEnemy39;
		wait(20);
	}
	wait(60);
	spawnEnemy40(150, 0, 150, 300);
	spawnEnemy40(width - 150, 0, width - 150, 300);
	waitForClearScreen;
	wait(60);
	descent (i in 0 .. 4) {
		spawnEnemy40(30 + 60 * i, height, 30 + 60 * i, height - 50);
		spawnEnemy40(width - 30 - 60 * i, 0, width - 30 - 60 * i, 50);
		wait(90);
	}
	waitForClearScreen;
	wait(300);
	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	// The Boss
	TDialogue(dialogueData);
	TBossPre;
	TDialogue(dialogueData2);
	TBossPost;
}

// 64, 0, 96, 32

task spawnEnemy39 {
	let enemy = enemyOf(rand(0, GetStgFrameWidth), 0, 1.5, 90,
		"./../resource/enemy.png", 0, 48, 16, 72,
		200, 100, 100, 8, 8,
		[5, 5, truncate(rand(0, 1) + 0.1), 0, 1, 1, 1, 1]);
	wait(60);
	let d = getDifficulty;
	let spread = truncate(d / 2);
	let speed = 2.70 + 0.3 * d;
	if (!Obj_IsDeleted(enemy)) {
		let a = GetAngleToPlayer(enemy);
		ascent (i in 0 .. 12) {
			ascent (j in -spread .. spread + 1) {
				CreateShotOA1(enemy, speed * (1 + 0.1 * i), a + 10 * j, DS_KUNAI_YELLOW, 0);
			}
		}
		ObjMove_SetAngle(enemy, 180 * randIntH(0, 2));
	}
}

// I brought the 64-bit edition of Python, when this computer uses 32-bit.

task spawnEnemy40(x1, y1, x2, y2) {
	let enemy = enemyOf(x1, y1, 0, 0,
		"./../resource/enemy.png", 0, 48, 16, 72,
		600, 100, 100, 8, 8,
		[20, 20, truncate(rand(0, 1) + 0.1), 0, 5, 5, 5, 5]);
	ObjMove_SetDestAtFrame(enemy, x2, y2, 60);
	wait(60);
	let p = 24 - 2 * getDifficulty;
	let c = 0;
	while (!Obj_IsDeleted(enemy) && c < 600) {
		let x = ObjMove_GetX(enemy);
		let y = ObjMove_GetY(enemy);
		let a = rand(-1, 1);
		let b = rand(-1, 1);
		CreateShotB2(x + rand(-5, 5), y + rand(-5, 5), a, -0.5, 0, 0.01, 5 * signum(a), 5, DS_ICE_SKY, 30);
		CreateShotB2(x + rand(-5, 5), y + rand(-5, 5), b, 0.5, 0, -0.01, 5 * signum(b), -5, DS_ICE_SKY, 30);
		wait(p);
		c += p;
	}
	Obj_Delete(enemy);
}

task spawnEnemy41(x, y, speed, angle) {
	let enemy = enemyOf(x, y, speed, angle,
		"./../resource/enemy.png", 0, 32, 16, 48,
		400, 100, 100, 8, 8,
		[10, 10, 0, truncate(rand(0, 1.25)), 3, 3, 3, 3]);
	let d = getDifficulty;
	let sp = 1.4 + 0.2 * d;
	let period = 90 - 2 * d;
	while (!Obj_IsDeleted(enemy)) {
		CreateShotOA1(enemy, sp * rand(1, 1.4), rand(-60, -120), DS_ICE_ORANGE, 0);
		CreateShotOA1(enemy, sp * rand(1, 1.4), rand(60, 120), DS_ICE_ORANGE, 0);
		wait(rand(0, 2 * period));
	}
}

task spawnEnemies41(x, y, speed, angle, count, period) {
	loop (count) {
		spawnEnemy41(x, y, speed, angle);
		wait(period);
	}
}

task spawnEnemy42(x1, y1, x2, y2, a0, s) {
	let enemy = enemyOf(x1, y1, 0, 0,
		"./../resource/enemy.png", 0, 48, 16, 72,
		750, 100, 100, 8, 8,
		[25, 25, 0, 0, 5, 5, 5, 5]);
	ObjMove_SetDestAtFrame(enemy, x2, y2, 60);
	wait(60);
	let angle = a0;
	let d = getDifficulty;
	let width = 20 + 4 * d;
	let height = 1 + truncate(d / 2);
	loop (width) {
		ascent (i in 0 .. height) {
			CreateShotOA1(enemy, 2 + 0.25 * height, angle, DS_RICE_M_PURPLE, 0);
		}
		angle += 360 * s / width;
		wait(120 / width);
	}
	ObjMove_SetAngle(enemy, 180 * randIntH(0, 2));
	ObjMove_SetSpeed(enemy, 5);
}

task spawnEnemy43(x, y, angle) {
	let enemy = enemyOf(x + rand(-10, 10), y, 2, angle,
		"./../resource/enemy.png", 0, 32, 16, 48,
		250, 100, 100, 8, 8,
		[10, 10, 0, 0, 2, 2, 2, 2]);
	let d = getDifficulty;
	let p = 0.07 + 0.01 * d;
	while (!Obj_IsDeleted(enemy)) {
		if (rand(0, 1) < p) {
			CreateShotOA1(enemy, rand(1, 2.5), randomSign * 90, DS_ICE_RED, 0);
		}
		yield;
	}
}

task spawnEnemies43(x, y, angle, count, period) {
	loop (count) {
		spawnEnemy43(x, y, angle);
		wait(period);
	}
}

task spawnEnemy44(x1, y1, x2, y2, x3, y3) {
	let enemy = enemyOf(x1, y1, 0, 0,
		"./../resource/enemy.png", 0, 48, 16, 72,
		150, 100, 100, 8, 8,
		[5, 5, 0, 0, 1, 1, 1, 1]);
	let d = getDifficulty;
	let speed = 3 + 0.5 * d;
	let ways = 6 + 2 * d;
	ObjMove_SetDestAtFrame(enemy, x2, y2, 30);
	wait(30);
	let a = GetAngleToPlayer(enemy);
	loop (ways) {
		ascent (i in 0 .. 10) {
			CreateShotOA1(enemy, speed * (1 + 0.05 * i), a, DS_BALL_S_RED, 0);
		}
		a += 360 / ways;
	}
	wait(60);
	ObjMove_SetDestAtFrame(enemy, x3, y3, 30);
}

task spawnEnemy44a(x) {
	spawnEnemy44(x, -10, x, 30, x, -200);
}

task spawnEnemy44b(x) {
	spawnEnemy44(x, height + 10, x, height - 30, x, height + 200);
}