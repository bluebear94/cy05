#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St7 Sp1"]
#Text["First spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
let id = 46;
let level = 10;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"
#include "./bossfunc.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(10000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(80);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

task TWork {
	let width = GetStgFrameWidth;
	let height = GetStgFrameHeight;
	let d = getDifficulty;
	let p = 70 - 5 * d;
	let a = 4 + d;
	let t = 0;
	task bulletTask(shot, i, b, s) {
		wait(10 * i);
		if (GetPlayerY < height / 2) {
			ObjMove_SetAngle(shot, GetAngleToPlayer(shot));
			ObjMove_SetSpeed(shot, 8);
		} else {
			ObjMove_SetAngle(shot, b + 10 * s * i + a * sin(t));
			ObjMove_SetSpeed(shot, 1.5);
		}
	}
	task fork {
		while (!isDead) {
			let a = rand(0, 360);
			loop (12) {
				CreateShotOA1(objBoss, 3, a, DS_ICE_WHITE, 0);
				a += 30;
			}
			wait(p * 0.8);
		}
	}
	task fork2 {
		while (!isDead) {
			CreateStraightLaserA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), GetAngleToPlayer(objBoss), 600, 20, 0.5 * p, DS_BEAM_SKY, p);
			wait(p * 2);
		}
	}
	fork;
	fork2;
	while (!isDead) {
		ascent (i in 0 .. 10) {
			let shot1 = CreateShotOA1(objBoss, 3, 150, DS_ICE_SKY, 0);
			bulletTask(shot1, i, 0, 1);
			let shot2 = CreateShotOA1(objBoss, 3, 30, DS_ICE_SKY, 0);
			bulletTask(shot2, i, 180, -1);
			if (d >= 1) {
				let shot3 = CreateShotOA1(objBoss, 3.5, 130, DS_ICE_SKY, 0);
				bulletTask(shot3, i, 0, 1);
				let shot4 = CreateShotOA1(objBoss, 3.5, 50, DS_ICE_SKY, 0);
				bulletTask(shot4, i, 180, -1);
			}
			t += 3.17;
		}
		wait(p);
	}
}

task TWorkOD {
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let a = GetAngleToPlayer(objBoss);
		loop (8) {
			ascent (i in 0 .. 20) {
				CreateShotOA1(objBoss, 3 + 0.01 * i, a, DS_KUNAI_YELLOW, i / 2);
			}
			a += 45;
		}
		wait(150);
	}
}
