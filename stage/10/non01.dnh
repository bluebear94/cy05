#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St7 Non1"]
#Text["First nonspell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
let id = 0;
let level = 10;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"
#include "./bossfunc.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(5000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(60);
	}
}

@Finalize {}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let d = getDifficulty;
	let speed = 4.5 + 0.3 * d;
	let period = 40 - 3 * d;
	let w = 3 + 0.2 * d;
	let streamw = truncate(d / 2);
	while (!isDead) {
		loop (3) {
			let a = GetAngleToPlayer(objBoss);
			ascent (i in 1 .. 10) {
				if (isDead) {return;}
				let a2 = a - w * i / 2;
				loop (i) {
					CreateShotOA1(objBoss, speed, a2, DS_ICE_BLUE, 0);
					a2 += w;
				}
				wait(2);
			}
			wait(period);
		}
		let a = rand(0, 360);
		ascent (i in -streamw .. streamw + 1) {
			loop (60) {
				let s = CreateShotOA1(objBoss, 3, a, DS_ICE_SKY, 0);
				ObjMove_AddPatternA4(s, 40, 4, 5 * i, -0.02, 0, 2.4, GetPlayerObjectID, DS_ICE_SKY);
				a += 6;
			}
		}
		wait(period);
	}
}

task TWorkOD {
	let width = GetStgFrameWidth;
	let height = GetStgFrameHeight;
	let d = getDifficulty;
	let period = 30 - 3 * getDifficulty;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		yield;
	}
}
