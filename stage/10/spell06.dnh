#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 51;
let level = 10;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(6000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(60);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

function cl(x0, y0, x1, y1, dt) {
	DelayLaser(x0, y0, getAngleTwoPoints(x0, y0, x1, y1), getDistanceTwoPoints(x0, y0, x1, y1),
		24, dt, DS_BEAM_WHITE, dt);
}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let x = ObjMove_GetX(objBoss);
	let y = ObjMove_GetY(objBoss);
	let d = getDifficulty;
	let bc = 10 + 2 * d;
	let bsp = 3.5 + 0.2 * d;
	let p = 0.4 + 0.1 * d;
	while (!isDead) {
		let a = GetAngleToPlayer(objBoss);
		ascent (i in -bc .. bc + 1) {
			if (isDead) {return;}
			let da = 60 * i / bc;
			let sp = bsp + 0.01 * da;
			ascent (j in 0 .. 4) {
				CreateShotOA1(objBoss, sp * (1 + 0.1 * j), a + da, DS_RICE_S_YELLOW, j);
				CreateShotOA1(objBoss, sp * (1 + 0.1 * j), a - da, DS_RICE_S_YELLOW, j);
			}
			wait(50 / bc);
		}
		x = GetPlayerX;
		y = GetPlayerY;
		cl(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), x, y, 90);
		wait(60);
		ObjMove_SetDestAtFrame(objBoss, x, y, 30);
		loop (30) {
			if (isDead) {return;}
			if (rand(0, 1) < p) {
				CreateShotOA1(objBoss, rand(0.1, 0.3), rand(0, 360), DS_BALL_M_RED + randIntH(0, 8), 0);
			}
			yield;
		}
	}
}