#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 22;
let level = 6;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(9000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(90);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let d = getDifficulty;
	let rayCount = min(6 + 2 * d, 12);
	let interval = 360 / rayCount;
	let t = 0;
	TWorkOD;
	TWorkOD2;
	TWorkOD3;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let angle = 90 + 0.5 * interval * (1 + sin(0.5 * t) + 0.2 * sin(10 * t));
		loop (rayCount) {
			CreateShotOA1(objBoss, 4, angle, DS_ICE_SKY, 0);
			angle += interval;
		}
		t++;
		yield;
	}
}

task TWorkOD {
	let d = getDifficulty;
	let shots = 4 + d;
	let incAngle = 360 / shots;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let angle = rand(0, 360);
		let s = randomSign;
		ascent (i in 0 .. shots) {
			ascent (j in 0 .. 10) {
				CreateShotOA1(objBoss, 3, angle + incAngle * i + s * j, DS_ICE_RED, j * 2);
			}
		}
		wait(90);
	}
}

task TWorkOD2 {
	let d = getDifficulty;
	let period = 60 - 4 * d;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		loop (10) {
			CreateShotB2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), rand(-2, 2), rand(-2, 0), 0, 0.02, 0, 2, DS_BALL_S_WHITE, 0);
		}
		wait(period);
	}
}

task TWorkOD3 {
	let d = getDifficulty;
	let count = 1 + truncate(d / 2);
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		loop (count) {
			CreateShotA1(0, rand(0, height), 2.4, 0, DS_SCALE_GREEN, 0);
			CreateShotA1(width, rand(0, height), 2.4, 180, DS_SCALE_GREEN, 0);
		}
		wait(20);
	}
}