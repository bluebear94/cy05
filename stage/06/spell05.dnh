#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 24;
let level = 6;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(20000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(60);
	}
	case (EV_REQUEST_IS_DURABLE_SPELL) {
		SetScriptResult(true);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let d = getDifficulty;
	let branchCount = 6 + 2 * getDifficulty;
	let p = 0.01 + 0.01 * d;
	let theta = 0;
	let phi = 0;
	//TWorkOD;
	ObjEnemy_SetDamageRate(objBoss, 0, 0);
	loop (3300) {
		ascent (i in 0 .. branchCount) {
			let thetaP = theta + 360 * i / branchCount;
			CreateShotA1(cx + cx * cos(thetaP), cy + cx * sin(thetaP),
				4, thetaP + 135 + 25 * sin(phi),
				DS_RICE_S_SKY, 0);
		}
		theta++;
		phi += 2.9;
		let alpha = rand(0, 360);
		if (rand(0, 1) < p) {
			CreateShotA1(cx + cx * cos(alpha), cy + cx * sin(alpha),
				rand(0.5, 1), alpha + 180 * truncate(rand(0, 2)),
				[DS_BILL_RED, DS_BILL_YELLOW, DS_BILL_GREEN, DS_BILL_BLUE][truncate(rand(0, 4))], 0);
			p += 0.000025;
		}
		yield;
	}
	ObjEnemy_SetDamageRate(objBoss, 100, 60);
}
