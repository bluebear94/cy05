#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["St1 Sp3"]
#Text["Third spell!"]
#Player["./../../player/player0.dnh"]
#System["./../../system/system.dnh"]
#include "./../../prologue.dnh"
#include "./../../player/shotc.dnh"
let id = 36;
let level = 8;
#include "./../spell_header.dnh"
#include "./render_boss.dnh"

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(5500);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(60);
	}
	case (EV_REQUEST_SPELL_SCORE) {
		SetScriptResult(spellBonus);
	}
}

@Finalize {}

let rate = 7.2436;

task TWork {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let d = getDifficulty;
	let iv = 1.5 + 0.25 * d;
	let t = 0;
	let u = 0;
	let amp = 10;
	let n = 1 + truncate(d / 2);
	let bases = [DS_BALL_S_BLUE, DS_BALL_S_GREEN, DS_BALL_S_YELLOW, DS_BALL_S_RED];
	move;
	while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let x = ObjMove_GetX(objBoss);
		let y = ObjMove_GetY(objBoss);
		let a = ObjMove_GetAngle(objBoss);
		let up = u + 5 * sin(1.13 * t);
		ascent (j in 1 .. n + 1) {
			let i = truncate(rand(0, 4));
			CreateShotA2(x - amp * j * sin(a), y + amp * j * cos(a),
				0.25 * iv, up * j, 0.01 * iv, iv, bases[i], 30);
			CreateShotA2(x + amp * j * sin(a), y - amp * j * cos(a),
				0.25 * iv, up * j + 180, 0.01 * iv, iv, bases[(i + 2) % 4], 30);
		}
		t += 40;
		u += rate;
		if (getDistanceToPlayer(objBoss) < 120) {
			loop (10) {
				CreateShotOA1(objBoss, rand(2, 4), rand(0, 360), DS_RICE_M_WHITE, 0);
			}
		}
		wait(5);
	}
}

task move {
	let height = GetStgFrameHeight;
	let width = GetStgFrameWidth;
	let cx = width / 2;
	let cy = height / 2;
	let th = 50;
	let d = getDifficulty;
	ObjMove_SetSpeed(objBoss, 2);
	ObjMove_SetAngle(objBoss, rand(0, 360));
	while (!isDead) {
		let x = ObjMove_GetX(objBoss);
		let y = ObjMove_GetY(objBoss);
		if (x < th) {
			ObjMove_SetAngle(objBoss, rand(-90, 90));
			rate = rand(6, 12);
			wait(5);
		} else if (x > width - th) {
			ObjMove_SetAngle(objBoss, rand(90, 270));
			rate = rand(6, 12);
			wait(5);
		}
		if (y < th) {
			ObjMove_SetAngle(objBoss, rand(0, 180));
			rate = rand(6, 12);
			wait(5);
		} else if (y > height - th) {
			ObjMove_SetAngle(objBoss, rand(-180, 0));
			rate = rand(6, 12);
			wait(5);
		}
		yield;
	}
}